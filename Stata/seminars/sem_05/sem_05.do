version 13 // version control
clear all // cleans memory from data
set more off // cancel pauses 
capture log close // closes open log file. На самом деле 2 команды: capture + log close

cd /Users/khaykingleb/Desktop/Personal/Work/HSE/Current_Semester/Stata-Course/seminars/sem_05 // sets working directory
log using log_2.log, replace
use RuFIGE.dta, clear

* Веса
// Q: Почему при работе с данными опросов фирм часто нужно использовать веса?
// A: Потому что зачастую выборка бывает нерепрезентативной — мы использоваем веса, дабы скорректировать это

// Q: Что будет, если делать анализ без весов?
mean employment [pw=weight] // опция pw — probability weights (веса для наблюдений)
mean employment
// A: Как видим, оценки очень сильно разнятся. Однако, именно первая оценка является истенной оценнкой среднего размера фирмы (в чел.) в структуре экономики страны (e.g. x% малых фирм, y% средних фирм и z% крупных фирм)
// Поскольку z часто очень мало — e.g. в России z около 3%, — а добавление только 30 крупных фирм на 1000 фирм в общей выборке не дадут состоятельных оценок, необходимо увеличить количество крупных фирм (то есть сделать выборку нерепрезентативной для улучшения оценок) и обязательно использовать веса для дальнейшей корректировки: w_z << 1, а w_x > 1.

// На примере другой переменной
tab B14 // смотрим, нет ли "затрудняюсь ответить" и тд (как видим, уже удалили такие ответы)
mean B14 [pw=weight]
mean B14

* Чувствительные вопросы
// E.g. не все фирмы захотят отвечать финансовые вопросы или будут отвечать на него нечестно, так как могут уклоняться от налоговых отчислений и т.д.

tab B14, miss // miss — опция, которая показывает и пропуски
// => Почти половина не ответила на вопрос о выручке
tab B30, miss // Видим, что почти все ответили на вопрос, так как этот вопрос нечувствительный
tab B49, miss // Видим, что почти все ответили на вопрос, так как этот вопрос нечувствительный

tab hire_competitive // 0 — указали один или два неконкурентных фактора (e.g. наняли по связям), 1  — указали два конкурентных фактора

* Регрессионный анализ: децентрализация и экономические результаты фирм
// Хотим протестировать гипотезу о том, что децентрализованные фирмы более экономически успешны в России

gen decentr=. // Создаем переменную (сначала с пропусками)
replace decentr=0 if B30==1
replace decentr=1 if B30==2

tab decentr
 
// Q: Как оценить долю децентрализованных фирм в России?
// A:
mean decentr [pw=weight]
// => Только около 17% фирм в России децентрализованные

// Будем исследовать связь между децентрализацией и инвестициями фирм
tab B49 // Осуществляло ли ваше предприятие в 2011-2013 годах инвестиции в основные фонды?
gen invest =.
replace invest=1 if B49==1
replace invest=0 if B49==2

// Проверим, влияет ли децентрализация на то, что фирме проще инвестировать?
// 1) Посмотрим на корреляцию
pwcorr invest decentr, sig // опция sig показывет статистическую значимость
// Как видим, корреляция значима

// Q: Означает ли значимая корреляция между двумя переменными наличие причинно-следственной связи между ними? 
// A: Нет. Возможно на них обоих влияет третий фактор, так что между invest и decentr нет прямой зависимости. E.g. более крупные фирмы более децнтрализованнее и богаче. Или трертий фактор — сектор.

// 2) Построим пробит-модель
probit invest decentr [pw=weight] 
// Как видим, коэффициент значим. Однако то, что мы сделали, — это не очень круто, потому что данная реализация тоже в некотором смысле корреляция в другом виде.
// => Введем контролирующие переменные в регрессионном анализе.

gen log_employment = log(employment)
probit invest decentr log_employment i.sector i.region [pw=weight]

// Как видим, коэф при децентрализации теперь НЕ значим => значит, корреляция между децентрализацией и инвестициями была следствием третих факторов.

// Однако, возможно различие между двумя типами децентрализации: делегированием сотрудникам, нанятым конкурентно, и делегированием сотрудникам, нанятым по знакомству. То есть по факту существование фирм делигированных по западным концепциям и псевдо-делигированных (на самом деле, по определению такими не являющимися).
gen decentr_hiring=.
replace decentr_hiring=0 if decentr==0
replace decentr_hiring=1 if decentr==1 & hire_competitive==0 // децентрализованные фирмы, в которые нанимают по знакомству
replace decentr_hiring=2 if decentr==1 & hire_competitive==1 // децентрализованные фирмы, в которые нанимают по конкуренции (западное понимание децентрализованных фирм)
tab decentr_hiring

// Зададим лейблы
label define l_decentr_hiring 0 "Centralized" 1 "Decentralized & hire through connections" 2 "Decentralized & hire competitively"
label val decentr_hiring l_decentr_hiring
tab decentr_hiring

// Проверяем наше предположение
probit invest i.decentr_hiring log_employment i.sector i.region [pw=weight]
// Видим, что коэффициент не значим для децентрализованных фирм, в которых нанимают по знакомству, и значим для децентрализованных фирмы, в которых нанимают по конкуренции. Следовательно, децентрализованные фирмы, в которых нанимают по конкуренции, чаще осуществляют инвестиции.

// Однако, не спешим констатировать, что наш предварительный вывод истинен: надо добавить еще больше контрольных переменных (третих факторов)
probit invest i.decentr_hiring log_employment log_age share_edu management i.sector i.region [pw=weight]
// Результат такой же
probit invest i.decentr_hiring log_employment log_age share_edu management state_property foreign_property i.sector i.region [pw=weight]
// Результат такой же
probit invest i.decentr_hiring log_employment log_age share_edu management state_property foreign_property i.location i.sector i.region [pw=weight]
// Результат такой же

// Следовательно, наш вывод, скорее всего, верен. Тем не менее, мы не знаем влияет ли, децентрализация на инвестиции или наоборот.
