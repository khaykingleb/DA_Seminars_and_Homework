version 13 // version control
clear all // cleans memory from data
set more off // cancel pauses 
capture log close // closes open log file. На самом деле 2 команды: capture + log close

cd /Users/khaykingleb/Desktop/Personal/Work/HSE/Current_Semester/Stata-Course/seminars/sem_07 // sets working directory
log using log_2.log, replace
use data.dta, clear

// Смотрим на дискриптивные статистики.
sum

// Для оценки эластичности предложения труда нам нужна выборка людей трудоспособного возраста. Исключите из выборки людей младше 21 и старше 55 лет. Сколько людей осталось в выборке?
drop if age < 21 | age > 55 // удаляем людей нетрудоспособного возраста
drop if year != 2003 // у нас много годов в выборке
sum, detail // как минимум четверть человек трудоспособного возраста  в 2003 не имеет трудового дохода

// Посчитайте долю людей, кот имеют нетрудовой доход.
count if nonlabinc > 0 & nonlabinc != . // 3960
display 3960/6466 //  получили ответ

// Постройте индикатор наличия работы (1 – работает (включая тех, кто в отпусках), 0 – не работает). Какая доля людей в данной выборке имеет работу? Сколько часов в месяц в среднем они работают?
//recode wrknow (1/4 = 1) (5 = 0) // 1/4 — человек работает, 5 — человек не работает. Так переменная переделается в бинарную ⇒ Лучше так не делать.
recode wrknow (1/4 = 1) (5 = 0), gen(work)

// Постройте переменную почасовой зарплаты, приведите ее среднее.
sum hourlm if work == 1 // работают около 171 часов в месяц
display 170.5872
gen wage_h = wage / hourlm 
sum wage_h // 31 рубль в час в среднем получали

// Постройте переменную логарифма почасовой зарплаты. Постройте гистограммы почасовой зарплаты и логарифма почасовой зарплаты (команда hist). Чем отличаются распределения этих переменных? Почему в регрессии мы будем использовать логарифм?
gen lwage_h = ln(wage_h)
hist wage_h, saving(1.gph)  // вводим опцию saving(1.gph), если хотим сохранить в рабочей дериктории файл, где 1.gph — название файла
hist lwage_h, saving(2.gph) 
gr combine 1.gph 2.gph

// Q: Почему в регрессии будем использовать логарифм, а не обычные?
// А: Потому что у первого логнормальное распределение


/// Постройте переменную логарифма нетрудового дохода таким образом, что у людей с нулевым доходом будет нулевое значение переменной
gen l_nonlabinc = ln(1 + nonlabinc)
sum l_nonlabinc, detail

gen age_sq = age * age


* Оценка эластичности предложения труда на выборке занятых
reg hourlm lwage_h age age_sq educyears married kids_under3 kids3_6 kids7_13 l_nonlabinc

// Оцените ту же модель с поправкой на гетероскедастичность (робастные стандартные ошибки – опция robust). Изменились ли коэффициенты регрессии? Как изменились стандартные ошибки коэффициентов регрессии и их значимость?
reg hourlm lwage_h age age_sq educyears married kids_under3 kids3_6 kids7_13 l_nonlabinc, robust

// Коэф при lwage_h мы не мжем экономически интерпретировать как эластичность предложения труда. Следовательно, ее надо посчитать 
// Это получается elasticity = dh/dw * w/h = (dh/dln(w))/h = coef при lwage_h / h
display -14.06526 / 170.5872
// Как видим, росте з/п на 1% предложение труда упадет на 0.08%.

// Q: Является ли предложение труда эластичным по зарплате?
// A: Нет, так как < 1

// Q: Исходя из оцененной вами модели, как меняется предложение труда в часах с возрастом? Как влияет на предложение труда наличие детей разных возрастов?
// A: 1) Растет с убывающими темпами, так как первая производная положительна, а вторая — отрицательна; 2) Только дети возрастом до 3 лет влияют положительно на предложение труда при прочих равных.

// NB: поскольку ненулевые часы работы и зарплаты наблюдаются только для работающих респондентов, вы оценили регрессионную модель и эластичность на подвыборке работающих. 
// Q: Можно ли считать полученную оценку эластичности состоятельной? Почему?
// A: Нет, потому что наблюдения неслучайны: мы уже заранее выбираем те наблюдения, где люди выбрали, что хотят работать.


* Оценка эластичности предложения труда на полной выборке
// Чтобы оценить регрессионную модель на полной выборке, нужно присвоить переменной часы работы нулевые значения для неработающих, а также оценить модель зарплаты и построить предсказанные значения зарплаты для неработающих

// Строим новую переменную часы
gen hour_new = hourlm
replace hour_new = 0 if work == 0

reg lwage_h age age_sq educyears, robust

predict lw_x  // сохраняем в переменную предсказания

// Постройте новую переменную логарифма почасовой зарплаты, которая содержит реальные значения для работающих и предсказанные вами значения для неработающих
gen lwage_new = lwage_h
replace lwage_new = lw_x if work == 0 // Q: Зачем? A: Есть люди, которые просто не сообщили нам з/п и то, что они работают

reg hour_new lwage_new age age_sq educyears married kids_under3 kids3_6 kids7_13 l_nonlabinc, robust

// Q: Как изменилось количество наблюдений в регрессии по сравнению с пунктом 3.1? 
// A: Выросло с 3... до 4... тысяч

// Посчитайте коэффициент эластичности предложения труда из модели. Как изменилась оценка эластичности по сравнению с предыдущей моделью? 
display -15.77557 / 170.5872
// Эластичность немного подросла


* Модель Тобит
// Q: Оценка модели предложения труда на полной выборке линейным методом МНК является некорректной. Почему?
// A: Когда оцениваем модель предложения труда для всей выборки, в зависимой переменной много нулей (цензурированная переменная или угловое решение). Проблемы использования линейной модели (МНК) в этом случае:
// • Структурная модель предложения труда дает нелинейную зависимость часов работы от зарплаты 
// • МНК дает отрицательные предсказанные значения часов работы для некоторых наблюдений
// • Распределение зависимой переменной не нормальное (одно из предположений МНК, хотя для больших выборок необязательно)
// Альтернатива – нелинейная модель Тобит

// Оцените модель для полной выборки методом Тобит
hist hour_new // Выборка цензирируема слева
tobit hour_new lwage_new age age_sq educyears married kids3_6 kids7_13 l_nonlabinc, ll // пишем в опции, что выборка цензурирована слево

// Считаем предельный эффект в точке среднего
mfx

// Считаем более состоятельную оценку эластичности
display -16.64761 / 170.5872
